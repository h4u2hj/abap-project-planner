@Metadata.layer: #CORE
@UI: {
  headerInfo: {
    typeName: 'Project',
    typeNamePlural: 'Projects',
    title: { type: #STANDARD, value: 'Name'},
    description: { type: #STANDARD, value: 'Description'}
  }
}
annotate view ZC_RAP_PROJECT with
{
  @UI.facet:
     [
     {
       label: 'General Information',
       type: #COLLECTION,
       id: 'GeneralInfo',
       position: 10
     },
     {
        label: 'Administration',
        type: #COLLECTION,
        id: 'AdminIdentification',
        position: 20
     },
     {
       id: 'idIdentification',
       type: #IDENTIFICATION_REFERENCE ,
       label: 'Project Plan Details',
       parentId: 'GeneralInfo',
       position: 10
     },
     {
        purpose: #STANDARD,
        position: 20,
        type: #FIELDGROUP_REFERENCE,
        parentId: 'GeneralInfo',
        label: 'Dates',
        targetQualifier: 'DatesGroup'
     },
     {
        purpose: #STANDARD,
        position: 10,
        type: #FIELDGROUP_REFERENCE,
        parentId: 'AdminIdentification',
        targetQualifier: 'AdminGroup'
     },
     {
        purpose: #HEADER,
        position: 10,
        type: #DATAPOINT_REFERENCE,
        targetQualifier: 'StatusData'
     }]

  @UI: {
  lineItem: [ { exclude: true } ,
  {
      type: #FOR_ACTION,
      dataAction: 'checkStartedInTime' ,
      label: 'Check Days till Start'
  } ] ,
  identification: [ { position: 1, label: 'ID' } ,
  {
      type: #FOR_ACTION,
      dataAction: 'checkStartedInTime',
      label: 'Check Days till Start'
  }]}

  @UI.hidden: true
  ID;

  @UI.lineItem: [ {
    position: 10 ,
    importance: #MEDIUM,
    label: 'Name'
  } ]
  @UI.identification: [ {
    position: 10 ,
    label: 'Name'
  } ]
  @Consumption.filter.hidden: true
  Name;

  @UI.identification: [ {
    position: 20 ,
    label: 'Description'
  } ]
  @Consumption.filter.hidden: true
  Description;

  @UI.lineItem: [ {
    position: 30 ,
    importance: #MEDIUM,
    label: 'Type'
  } ]
  @UI.identification: [ {
    position: 30 ,
    label: 'Type'
  } ]
  @UI.selectionField: [{position: 10}]
  Type;

  @UI.lineItem: [ {
    position: 40 ,
    importance: #MEDIUM,
    label: 'Status',
    criticality: 'StatusCriticality',
    cssDefault.width: '8%'
  } ]
  @UI.identification: [ {
    position: 40 ,
    label: 'Status',
    criticality: 'StatusCriticality'
  } ]
  @UI.selectionField: [{position: 20}]
  @UI.textArrangement: #TEXT_ONLY
  @UI.dataPoint: { qualifier: 'StatusData', title: 'Status', criticality: 'StatusCriticality' }
  Status;

  @UI.lineItem: [ {
    position: 50 ,
    importance: #MEDIUM,
    label: 'Cost'
  } ]
  @UI.identification: [ {
    position: 50 ,
    label: 'Cost'
  } ]
  @Consumption.filter.hidden: true
  Cost;
  
   @UI.lineItem: [ {
    position: 70 ,
    importance: #MEDIUM,
    label: 'Budget'
  } ]
  @UI.identification: [ {
    position: 70 ,
    label: 'Budget'
  } ]
  @Consumption.filter.hidden: true
  Budget;

  @UI.hidden: true
  Currency;

  @Consumption.filter.selectionType: #SINGLE
  @UI.lineItem: [ {
    position: 90 ,
    importance: #MEDIUM,
    label: 'Planned Start Date'
  } ]
  @UI.selectionField: [{position: 30}]
  @UI.fieldGroup: [{qualifier: 'DatesGroup', position: 10, label: 'Planned Start Date' }]
  PlannedStartDate;

  @UI.lineItem: [ {
    position: 100 ,
    importance: #MEDIUM,
    label: 'Planned End Date'
  } ]
  @UI.selectionField: [{position: 40}]
  @UI.fieldGroup: [{qualifier: 'DatesGroup', position: 20, label: 'Planned End Date' }]
  @Consumption.filter.selectionType: #SINGLE
  PlannedEndDate;

  @UI.lineItem: [ {
    position: 110 ,
    importance: #MEDIUM,
    label: 'Days till Start'
  } ]
  @UI.fieldGroup: [{qualifier: 'DatesGroup', position: 30, label: 'Days till Start' }]
  @Consumption.filter.hidden: true
  DaysToStart;

  @UI.fieldGroup: [{qualifier: 'AdminGroup', position: 10 }]
  CreatedBy;

  @UI.fieldGroup: [{qualifier: 'AdminGroup', position: 20 }]
  @Consumption.filter.selectionType: #SINGLE
  CreatedAt;

  @UI.fieldGroup: [{qualifier: 'AdminGroup', position: 30 }]
  LastChangedBy;

  @UI.fieldGroup: [{qualifier: 'AdminGroup', position: 40 }]
  @Consumption.filter.selectionType: #SINGLE
  LastChangedAt;

  @UI.hidden: true
  StatusText;
  
  @Consumption.filter.hidden: true
  StatusId;

  @UI.hidden: true
  StatusCriticality;

  @UI.hidden: true
  LocalLastChanged;

}